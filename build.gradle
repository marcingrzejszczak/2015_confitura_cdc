buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE")
		classpath 'io.codearte.accurest:accurest-gradle-plugin:0.3.0'
	}
}

ext {
	spockVersion = '0.7-groovy-2.0'
	restAssuredVersion = '2.4.0'
	accurestStubsBaseDirectory = 'src/test/resources/stubs'
}

configure(project(':fraudDetectionService')) {
	task copyStubs(type: Copy) {
		from("${project(':stubs').projectDir.absolutePath}/mappings/fraudDetectionService")
		into "${accurestStubsBaseDirectory}/fraud"
	}
}

configure(project(':loanApplicationService')) {
	task copyStubs(type: Copy) {
		from("${project(':stubs').projectDir.absolutePath}/mappings/loanApplicationService")
		into "${accurestStubsBaseDirectory}/loan"
	}
	task copyCollaboratorStubs(type: Copy) {
		from("${project(':stubs').projectDir.absolutePath}/mappings/fraudDetectionService")
		into "src/test/resources/mappings"
	}

	copyStubs.dependsOn('copyCollaboratorStubs')

}

subprojects {
	apply plugin: 'groovy'


	repositories {
		mavenCentral()
		mavenLocal()
	}

	dependencies {
		testCompile "org.codehaus.groovy:groovy-all:2.3.7"
		testCompile "org.spockframework:spock-core:$spockVersion"
		testCompile("junit:junit:4.12")
		testCompile('com.github.tomakehurst:wiremock:1.52') {
			exclude group: 'org.mortbay.jetty', module: 'servlet-api'
		}
	}
}

configure([project(':fraudDetectionService'), project(':loanApplicationService')]) {
	apply plugin: 'spring-boot'
	apply plugin: 'accurest'

	accurest {
		targetFramework = 'Spock'
		testMode = 'MockMvc'
		baseClassForTests = 'com.blogspot.toomuchcoding.MvcSpec'
		stubsBaseDirectory = accurestStubsBaseDirectory
	}

	generateAccurest.dependsOn('copyStubs')

	jar {
		version = '0.0.1'
	}

	dependencies {
		compile("org.springframework.boot:spring-boot-starter-web") {
			exclude module: "spring-boot-starter-tomcat"
		}
		compile("org.springframework.boot:spring-boot-starter-jetty")
		compile("org.springframework.boot:spring-boot-starter-actuator")

		testRuntime "org.spockframework:spock-spring:$spockVersion"
		testCompile "org.springframework:spring-test"
		testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
		testCompile "com.jayway.restassured:spring-mock-mvc:$restAssuredVersion"
	}

	task cleanup(type: Delete) {
		delete 'src/test/resources/mappings', 'src/test/resources/stubs'
	}

	clean.dependsOn('cleanup')

}
